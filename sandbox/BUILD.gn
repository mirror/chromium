# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//sandbox/features.gni")
import("//testing/test.gni")

# Several targets want to include this header file. We separate it out
# here so multiple targets can depend on it.
source_set("sandbox_export") {
  sources = [
    "sandbox_export.h",
  ]
}

source_set("sandbox") {
  sources = [
    "policy/sandbox_type.cc",
    "policy/sandbox_type.h",
  ]
  defines = [ "SANDBOX_IMPLEMENTATION" ]
  if (is_win) {
    public_deps = [
      "//sandbox/win:sandbox",
    ]
  } else if (is_mac) {
    public_deps = [
      "//sandbox/mac:seatbelt",
    ]
  } else if (is_linux || is_android) {
    public_deps = [
      "//sandbox/linux:sandbox",
    ]
  }
}

test("sandbox_unittests") {
  sources = [
    "policy/sandbox_type_unittest.cc",
  ]
  deps = [
    "//base/test:run_all_unittests",
    "//sandbox:sandbox",
    "//testing/gtest",
  ]
  if (is_mac) {
    deps += [ "//sandbox/mac:sandbox_unittests_sources" ]
  } else if (is_linux || is_android) {
    deps += [
      "//build/config:exe_and_shlib_deps",
      "//sandbox/linux:sandbox_unittests_sources",
    ]
    if (is_android) {
      use_raw_android_executable = true
    }
  }
}

buildflag_header("sandbox_features") {
  header = "sandbox_features.h"
  flags = [ "USE_SECCOMP_BPF=$use_seccomp_bpf" ]
}
