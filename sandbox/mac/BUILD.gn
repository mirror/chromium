# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/mac/mac_sdk.gni")
import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("seatbelt_proto") {
  visibility = [ ":*" ]
  sources = [
    "seatbelt.proto",
  ]
}

component("seatbelt") {
  sources = [
    "sandbox_compiler.cc",
    "sandbox_compiler.h",
    "seatbelt.cc",
    "seatbelt.h",
    "seatbelt_exec.cc",
    "seatbelt_exec.h",
    "seatbelt_export.h",
  ]
  defines = [ "SEATBELT_IMPLEMENTATION" ]
  libs = [ "sandbox" ]
  public_deps = [
    ":seatbelt_proto",
    "//base",
    "//third_party/protobuf:protobuf_lite",
  ]
}

source_set("sandbox_unittests_sources") {
  testonly = true
  sources = [
    "sandbox_mac_compiler_unittest.mm",
    "sandbox_mac_compiler_v2_unittest.mm",
    "sandbox_mac_seatbelt_exec_unittest.cc",
  ]
  libs = [
    "sandbox",
    "CoreFoundation.framework",
    "Foundation.framework",
  ]
  deps = [
    ":seatbelt",
    "//base",
    "//sandbox",
    "//testing/gtest",
  ]
}
