# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/config.gni")
import("//ios/public/provider/chrome/browser/build_config.gni")
import("//testing/test.gni")

group("all_tests") {
  testonly = true
  deps = [
    ":ios_chrome_unittests",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "block_cleanup_test.h",
    "block_cleanup_test.mm",
    "ios_chrome_scoped_testing_chrome_browser_provider.h",
    "ios_chrome_scoped_testing_chrome_browser_provider.mm",
    "ios_chrome_scoped_testing_local_state.cc",
    "ios_chrome_scoped_testing_local_state.h",
    "ios_chrome_unit_test_suite.h",
    "ios_chrome_unit_test_suite.mm",
    "testing_application_context.h",
    "testing_application_context.mm",
  ]

  deps = [
    "//base",
    "//components/content_settings/core/common",
    "//components/network_time",
    "//components/prefs",
    "//components/prefs:test_support",
    "//ios/chrome/browser",
    "//ios/chrome/browser/browser_state:browser_state_impl",
    "//ios/chrome/browser/prefs:browser_prefs",
    "//ios/public/provider/chrome/browser",
    "//ios/public/provider/chrome/browser:test_support",
    "//ios/web",
    "//ios/web:test_support",
    "//net",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base",
    "//url",
  ]
}

source_set("run_all_unittests") {
  testonly = true
  sources = [
    "run_all_unittests.cc",
  ]
  deps = [
    ":test_support",
    "//base",
    "//base/test:test_support",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "google_toolbox_unittest.mm",
  ]
  deps = [
    "//base",
    "//testing/gtest",
    "//third_party/google_toolbox_for_mac",
  ]
}

test("ios_chrome_unittests") {
  deps = [
    # Ensure that all unit tests are run and packed resources available.
    ":run_all_unittests",
    ios_packed_resources_target,

    # Add unit_tests target here.
    ":unit_tests",
    "//ios/chrome/app:unit_tests",
    "//ios/chrome/app/application_delegate:unit_tests",
    "//ios/chrome/browser:unit_tests",
    "//ios/chrome/browser/browsing_data:unit_tests",
    "//ios/chrome/browser/device_sharing:unit_tests",
    "//ios/chrome/browser/favicon:unit_tests",
    "//ios/chrome/browser/geolocation:unit_tests",
    "//ios/chrome/browser/itunes_links:unit_tests",
    "//ios/chrome/browser/metrics:unit_tests",
    "//ios/chrome/browser/native_app_launcher:unit_tests",
    "//ios/chrome/browser/net:unit_tests",
    "//ios/chrome/browser/passwords:unit_tests",
    "//ios/chrome/browser/reading_list:unit_tests",
    "//ios/chrome/browser/signin:unit_tests",
    "//ios/chrome/browser/snapshots:unit_tests",
    "//ios/chrome/browser/ssl:unit_tests",
    "//ios/chrome/browser/suggestions:unit_tests",
    "//ios/chrome/browser/sync:unit_tests",
    "//ios/chrome/browser/translate:unit_tests",
    "//ios/chrome/browser/u2f:unit_tests",
    "//ios/chrome/browser/ui:unit_tests",
    "//ios/chrome/browser/ui/alert_coordinator:unit_tests",
    "//ios/chrome/browser/ui/commands:unit_tests",
    "//ios/chrome/browser/ui/context_menu:unit_tests",
    "//ios/chrome/browser/ui/dialogs:unit_tests",
    "//ios/chrome/browser/ui/elements:unit_tests",
    "//ios/chrome/browser/ui/keyboard:unit_tests",
    "//ios/chrome/browser/ui/voice:unit_tests",
    "//ios/chrome/browser/update_client:unit_tests",
    "//ios/chrome/browser/voice:unit_tests",
    "//ios/chrome/browser/web:unit_tests",
    "//ios/chrome/browser/web_resource:unit_tests",
    "//ios/chrome/common:unit_tests",
    "//ios/chrome/test/base:unit_tests",
    "//ios/web/public/image_fetcher:unit_tests",
  ]

  assert_no_deps = ios_assert_no_deps
}
