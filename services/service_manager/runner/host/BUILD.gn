# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

group("host") {
  testonly = true

  deps = [
    ":lib",
    ":mojo_runner_host_unittests",
  ]
}

source_set("lib") {
  sources = [
    "child_process_host.cc",
    "child_process_host.h",
    "out_of_process_native_runner.cc",
    "out_of_process_native_runner.h",
  ]

  deps = [
    "//base:base_static",
    "//base:i18n",
    "//services/service_manager/public/cpp:sources",
    "//services/service_manager/public/cpp/standalone_service",
    "//services/service_manager/runner:init",
    "//services/service_manager/runner/common",
  ]

  public_deps = [
    "//base",
    "//mojo/edk/system",
    "//mojo/public/cpp/system",
    "//services/service_manager",
    "//services/service_manager/public/interfaces",
  ]

  if (is_linux && !is_android) {
    deps += [ "//sandbox/linux:sandbox_services" ]
  }
}

test("mojo_runner_host_unittests") {
  sources = [
    "child_process_host_unittest.cc",
    "host_unittests.cc",
  ]

  deps = [
    ":lib",
    "//base",
    "//base/test:test_support",
    "//mojo/edk/system",
    "//services/service_manager",
    "//services/service_manager/runner:init",
    "//services/service_manager/runner/common",
    "//testing/gtest",
  ]

  data_deps = [
    ":host_test_service",
  ]
}

service("host_test_service") {
  sources = [
    "host_test_service_main.cc",
  ]

  deps = [
    "//mojo/public/cpp/system",
    "//services/service_manager/public/cpp/standalone_service:main",
  ]

  data_deps = [
    ":host_test_service_manifest",
  ]
}

service_manifest("host_test_service_manifest") {
  name = "host_test_service"
  source = "host_test_service_manifest.json"
}
