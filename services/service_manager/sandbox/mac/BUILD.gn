# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("packaged_sb_files") {
  sources = [
    "$target_gen_dir/common.sb.cc",
    "$target_gen_dir/common.sb.h",
    "$target_gen_dir/gpu.sb.cc",
    "$target_gen_dir/gpu.sb.h",
    "$target_gen_dir/nacl_loader.sb.cc",
    "$target_gen_dir/nacl_loader.sb.h",
    "$target_gen_dir/ppapi.sb.cc",
    "$target_gen_dir/ppapi.sb.h",
    "$target_gen_dir/renderer_v2.sb.cc",
    "$target_gen_dir/renderer_v2.sb.h",
    "$target_gen_dir/utility.sb.cc",
    "$target_gen_dir/utility.sb.h",
  ]
  deps = [
    ":package_sb_files",
  ]
}

action_foreach("package_sb_files") {
  script = "package_sb_file.py"
  sources = [
    "common.sb",
    "gpu.sb",
    "nacl_loader.sb",
    "ppapi.sb",
    "renderer_v2.sb",
    "utility.sb",
  ]
  outputs = [
    "$target_gen_dir/{{source_name_part}}.h",
    "$target_gen_dir/{{source_name_part}}.cc",
  ]
  args = [
    "{{source}}",
    rebase_path("$target_gen_dir", root_build_dir),
  ]
}
