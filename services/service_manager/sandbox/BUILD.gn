# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

component("sandbox") {
  sources = [
    "export.h",
    "sandbox_type.cc",
    "sandbox_type.h",
    "switches.cc",
    "switches.h",

    # Duplicated here to keep private.
    "//services/service_manager/embedder/service_manager_embedder_export.h",
  ]
  defines = [ "SERVICE_MANAGER_SANDBOX_IMPL" ]
  public_deps = [
    "//services/service_manager/embedder:embedder_switches",
  ]
  deps = [
    "//base",
  ]
  if (is_linux) {
    sources += [
      "linux/bpf_base_policy.cc",
      "linux/bpf_base_policy.h",
      "linux/bpf_cdm_policy.cc",
      "linux/bpf_cdm_policy.h",
      "linux/bpf_cros_amd_gpu_policy.cc",
      "linux/bpf_cros_amd_gpu_policy.h",
      "linux/bpf_cros_arm_gpu_policy.cc",
      "linux/bpf_cros_arm_gpu_policy.h",
      "linux/bpf_gpu_policy.cc",
      "linux/bpf_gpu_policy.h",
      "linux/bpf_pdf_compositor_policy.cc",
      "linux/bpf_pdf_compositor_policy.h",
      "linux/bpf_ppapi_policy.cc",
      "linux/bpf_ppapi_policy.h",
      "linux/bpf_renderer_policy.cc",
      "linux/bpf_renderer_policy.h",
      "linux/bpf_utility_policy.cc",
      "linux/bpf_utility_policy.h",
      "linux/sandbox_debug_handling.cc",
      "linux/sandbox_debug_handling.h",
      "linux/sandbox_linux.cc",
      "linux/sandbox_linux.h",
      "linux/sandbox_seccomp_bpf.cc",
      "linux/sandbox_seccomp_bpf.h",
    ]
    deps += [ "//sandbox/linux:sandbox" ]
  }
  if (is_mac) {
    public_deps += [ "mac:packaged_sb_files" ]
  }
}
