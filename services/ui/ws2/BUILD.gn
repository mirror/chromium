# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")
import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//services/service_manager/public/tools/test/service_test.gni")

component("lib") {
  sources = [
    "ids.h",

    # TODO window_data should be internal and moved to a different target.
    "window_data.cc",
    "window_data.h",
    "window_host_frame_sink_client.cc",
    "window_host_frame_sink_client.h",
    "window_service.cc",
    "window_service.h",
    "window_service_client.cc",
    "window_service_client.h",
    "window_service_client_binding.cc",
    "window_service_client_binding.h",
    "window_service_delegate.cc",
    "window_service_delegate.h",
    "window_tree_factory.cc",
    "window_tree_factory.h",
  ]

  public_deps = [
    "//services/ui/common:mus_common",
    "//services/ui/public/interfaces",
    "//ui/aura",
  ]

  defines = [ "IS_WINDOW_SERVICE_IMPL" ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "test_change_tracker.cc",
    "test_change_tracker.h",
    "window_server_service_test_base.cc",
    "window_server_service_test_base.h",
  ]

  deps = [
    "//base",
    "//base/test:test_config",
    "//components/viz/test:test_support",
    "//mojo/common",
    "//mojo/public/cpp/bindings:bindings",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/ui/common:mus_common",
    "//services/ui/public/cpp",
    "//services/ui/public/interfaces",
    "//testing/gtest",
    "//ui/aura",
    "//ui/display:test_support",
    "//ui/gfx/geometry/mojo",
    "//ui/gl",
    "//ui/wm",
  ]

  data_deps = [
    "//services/ui/ime/test_ime_driver",
  ]
}

service_manifest("unittests_manifest") {
  name = "ui_ws2_service_unittests"
  source = "test_manifest.json"
}

catalog("tests_catalog") {
  testonly = true
  embedded_services = [ ":unittests_manifest" ]
  standalone_services = [ "//services/ui/test_ws:manifest" ]
}

source_set("tests") {
  testonly = true

  sources = [
    "window_tree_client_unittest.cc",
  ]

  deps = [
    ":lib",
    ":test_support",
    "//base",
    "//base/test:test_config",
    "//base/test:test_support",
    "//cc:cc",
    "//cc:test_support",
    "//components/viz/service/main",
    "//components/viz/test:test_support",
    "//gpu/ipc/client",
    "//mojo/public/cpp/bindings:bindings",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/service_manager/public/mojom",
    "//services/ui/common:mus_common",
    "//services/ui/common:task_runner_test_base",
    "//services/ui/public/cpp",
    "//services/ui/public/interfaces",
    "//testing/gtest",
    "//third_party/mesa:osmesa",
    "//ui/aura",
    "//ui/aura:test_support",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
    "//ui/gfx/geometry/mojo",
    "//ui/gl",
    "//ui/gl/init",
  ]

  data_deps = [
    "//services/ui/test_ws",
  ]
}
