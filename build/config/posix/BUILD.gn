# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/build_metadata.gni")
import("//build/config/sysroot.gni")

assert(is_posix)

# This is included by reference in the //build/config/compiler:runtime_library
# config that is applied to all targets. It is here to separate out the logic
# that is Posix-only. Please see that target for advice on what should go in
# :runtime_library vs. :compiler.
config("runtime_library") {
  if (!is_mac && sysroot != "") {
    # Pass the sysroot to all C compiler variants, the assembler, and linker.
    # If we don't care about build metadata, then just set the sysroot to an
    # absolute path. Some buildbots (read: codesearch) care about this.
    # See crbug.com/580103 for some discussion of why this was necessary.
    if (dont_embed_build_metadata) {
      cflags = [ "--sysroot=" + rebase_path(sysroot, root_build_dir) ]
    } else {
      cflags = [ "--sysroot=" + rebase_path(sysroot) ]
    }
    asmflags = cflags
    ldflags = cflags

    # Need to get some linker flags out of the sysroot.
    ldflags += [ exec_script("sysroot_ld_path.py",
                             [
                               rebase_path("//build/linux/sysroot_ld_path.sh",
                                           root_build_dir),
                               sysroot,
                             ],
                             "value") ]
  }
}
