# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//chrome/android/chrome_apk_version.gni")

if (!is_component_build) {
  template("remoting_apk_tmpl") {
    android_apk(target_name) {
      forward_variables_from(invoker, "*")

      version_name = chrome_apk_version_name
      version_code = chrome_apk_version_code
      android_manifest = "$root_gen_dir/remoting/android/AndroidManifest.xml"
      native_libs = [ "libremoting_client_jni.so" ]

      deps += [
        "//remoting/android:remoting_apk_manifest",
        "//remoting/android:remoting_client_jni",
      ]

      if (target_cpu == "arm") {
        deps += [ "//remoting/android:remoting_cardboard_extract_native_lib" ]
        native_libs += [ "libvrtoolkit.so" ]
      }
    }
  }
}
