# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Keep in sync with 'remoting_webapp_html' target in
# remoting/remoting_client.gypi.

import("//remoting/webapp/files.gni")

action("html") {
  script = "build-html.py"

  inputs = [ remoting_webapp_template_main ] + remoting_webapp_template_files +
           remoting_webapp_crd_main_html_all_js_files

  outputs = [
    "$target_gen_dir/main.html",
  ]

  # Template files are relative to this directory. This passes some template
  # files to the script, and the script reads templates from the files on disk.
  # They all have to be relative to the same directory. The GYP build made all
  # of these relative to the remoting directory, so this does the same.
  template_rel_dir = "//remoting"

  args = [
    rebase_path("$target_gen_dir/main.html", root_build_dir),
    rebase_path(remoting_webapp_template_main, root_build_dir),
  ]
  args += [
    "--template-dir",
    rebase_path(template_rel_dir, root_build_dir),
  ]
  args += [ "--templates" ] +
          rebase_path(remoting_webapp_template_files, template_rel_dir)
  args += [ "--js" ] + rebase_path(remoting_webapp_crd_main_html_all_js_files,
                                   template_rel_dir)
}
# TODO(GYP) wcs_sandbox.html, background.html, message_window.html
