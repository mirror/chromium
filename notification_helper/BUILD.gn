# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//chrome/process_version_rc_template.gni")
import("//testing/test.gni")

##------------------------------------------------------------------------------
## notification_helper
##------------------------------------------------------------------------------

if (is_win) {
  executable("notification_helper") {
    sources = [
      "notification_helper.cc",
      "notification_helper.rc",
    ]

    configs -= [ "//build/config/win:console" ]
    configs += [ "//build/config/win:windowed" ]

    deps = [
      ":lib",
      ":version_resources",
      "//base",
      "//build/win:default_exe_manifest",
      "//chrome/common:constants",
      "//chrome/install_static:primary_module",
      "//components/crash/content/app:run_as_crashpad_handler",
      "//content/public/common:static_switches",
    ]

    libs = [ "runtimeobject.lib" ]
  }

  source_set("lib") {
    visibility = [ ":*" ]
    sources = [
      "com_server_module.cc",
      "com_server_module.h",
      "notification_activator.cc",
      "notification_activator.h",
    ]

    deps = [
      "//base",
      "//chrome/install_static:install_static_util",
    ]
  }

  process_version_rc_template("version_resources") {
    sources = [
      "notification_helper_exe.ver",
    ]
    output = "$target_gen_dir/notification_helper_exe.rc"
  }

  ##------------------------------------------------------------------------------
  ## tests
  ##------------------------------------------------------------------------------

  test("notification_helper_unittests") {
    sources = [
      "com_server_module_unittest.cc",
      "notification_helper_process_unittest.cc",
    ]

    deps = [
      ":lib",
      "//base",
      "//base/test:run_all_unittests",
      "//chrome/install_static:install_static_util",
      "//chrome/install_static:primary_module",
      "//testing/gtest",
    ]

    libs = [ "runtimeobject.lib" ]
  }
}
