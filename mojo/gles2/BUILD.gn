# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("mojo_use_gles2") {
  defines = [ "MOJO_USE_GLES2_IMPL" ]
}

config("gles2_use_mojo") {
  defines = [ "GLES2_USE_MOJO" ]
}

component("gles2") {
  output_name = "mojo_gles2_impl"
  sources = [
    "gles2_impl.cc",
  ]

  defines = [
    "GL_GLEXT_PROTOTYPES",
    "MOJO_GLES2_IMPLEMENTATION",
  ]

  configs += [
    ":gles2_use_mojo",
    ":mojo_use_gles2",
  ]
  public_configs = [
    ":gles2_use_mojo",
    "//third_party/khronos:khronos_headers",
  ]
  all_dependent_configs = [ ":mojo_use_gles2" ]

  public_deps = [
    "//mojo/public/c/gles2",
  ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//components/mus/public/cpp",
    "//gpu/command_buffer/client:gles2_interface",
    "//mojo/public/c/system:for_component",
  ]

  include_dirs = [ ".." ]
}
