# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//mojo/system.gni")

assert(use_aura)

group("wm_flow" ) {
  deps = [
    ":app",
    ":embedded",
    ":init",
    ":wm_flow_wm",
  ]
}

# GYP version: mojo/mojo_examples.gypi:mojo_wm_flow_wm
shared_library("wm_flow_wm") {
  output_name = "mojo_wm_flow_wm"

  sources = [
    "wm/wm.cc",
  ]

  deps = [
    "//base",
    "//mojo/application",
    "//mojo/services/public/cpp/view_manager",
    "//mojo/services/window_manager:lib",
  ] + mojo_system_for_shared_library
}

# GYP version: mojo/mojo_examples.gypi:mojo_wm_flow_init
shared_library("init") {
  output_name = "mojo_wm_flow_init"

  sources = [
    "init/init.cc",
  ]

  deps = [
    "//base",
    "//mojo/application",
    "//mojo/services/public/interfaces/view_manager",
  ] + mojo_system_for_shared_library
}

# GYP version: mojo/mojo_examples.gypi:mojo_wm_flow_app
shared_library("app") {
  output_name = "mojo_wm_flow_app"

  sources = [
    "app/app.cc",
  ]

  deps = [
    ":embedder_bindings",
    ":embeddee_bindings",
    "//base",
    "//mojo/application",
    "//mojo/services/public/cpp/view_manager",
    "//mojo/services/window_manager:lib",
  ] + mojo_system_for_shared_library
}

# GYP version: mojo/mojo_examples.gypi:mojo_wm_flow_embedded
shared_library("embedded") {
  output_name = "mojo_wm_flow_embedded"

  sources = [
    "embedded/embedded.cc",
  ]

  deps = [
    ":embedder_bindings",
    ":embeddee_bindings",
    "//base",
    "//mojo/application",
    "//mojo/services/public/cpp/view_manager",
    "//mojo/services/window_manager:lib",
  ] + mojo_system_for_shared_library
}

# GYP version: mojo/mojo_examples.gypi:mojo_wm_flow_embedder_bindings
mojom("embedder_bindings") {
  sources = [
    "app/embedder.mojom",
  ]
}

# GYP version: mojo/mojo_examples.gypi:mojo_wm_flow_embeddee_bindings
mojom("embeddee_bindings") {
  sources = [
    "embedded/embeddee.mojom",
  ]
}
