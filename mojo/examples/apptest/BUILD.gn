# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

group("apptest") {
  testonly = true

  deps = [
    ":apptests",
    ":service",
  ]
}

# GYP version mojo/mojo_examples.gypi:mojo_example_service
shared_library("service") {
  output_name = "mojo_example_service"

  sources = [
    "example_service_application.cc",
    "example_service_application.h",
    "example_service_impl.cc",
    "example_service_impl.h",
  ]

  deps = [
    ":bindings",
    "//mojo/public/c/system:for_shared_library",
    "//mojo/public/cpp/application:standalone",
    "//mojo/public/cpp/utility"
  ]
}

# GYP version: mojo/mojo_examples.gypi:mojo_example_apptests
shared_library("apptests") {
  output_name = "mojo_example_apptests"

  testonly = true

  sources = [
    "example_apptest.cc",
    "example_client_application.cc",
    "example_client_application.h",
    "example_client_impl.cc",
    "example_client_impl.h",
  ]

   deps = [
    ":bindings",
    "//testing/gtest",
    "//mojo/public/c/system:for_shared_library",
    "//mojo/public/cpp/application:standalone",
    "//mojo/public/cpp/utility",
   ]
}

# GYP version: mojo/mojo_examples.gypi:mojo_example_service_bindings
mojom("bindings") {
  sources = [ "example_service.mojom" ]
}
