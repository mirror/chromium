# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":apptests",
    ":mojo_shell_unittests",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "capability_filter_test.cc",
    "capability_filter_test.h",
  ]

  deps = [
    ":interfaces",
    "//mojo/shell",
    "//mojo/shell/public/cpp",
    "//mojo/shell/public/interfaces",
    "//testing/gtest",
  ]
}

# TODO(beng): this target should just be called "unittests" but I am having
#             difficulty with the android _apk generator.
test("mojo_shell_unittests") {
  sources = [
    "capability_filter_unittest.cc",
    "loader_unittest.cc",
    "run_all_unittests.cc",
  ]

  deps = [
    ":interfaces",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//mojo/edk/system",
    "//mojo/public/cpp/system",
    "//mojo/shell",
    "//mojo/shell/background:lib",
    "//mojo/shell/background:main",
    "//mojo/shell/background/tests:unittests",
    "//mojo/shell/public/cpp",
    "//mojo/util:filename_util",
    "//testing/gtest",
    "//url",
  ]
}

mojom("interfaces") {
  sources = [
    "capability_filter_unittest.mojom",
    "test.mojom",
  ]
}

mojo_native_application("apptests") {
  output_name = "mojo_shell_apptests"
  testonly = true

  deps = [
    "//mojo/shell/tests/application_manager",
  ]

  data_deps = [
    "//mojo/shell/tests/connect",
  ]
}
