# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

source_set("application_manager") {
  testonly = true
  sources = [
    "application_manager_apptest.cc",
  ]

  deps = [
    ":interfaces",
    ":manifest",
    "//base",
    "//base/test:test_config",
    "//mojo/common:common_base",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/cpp:test_support",
    "//mojo/shell/public/interfaces",
  ]

  data_deps = [
    ":application_manager_apptest_driver",
    ":application_manager_apptest_target",
  ]
}

mojom("interfaces") {
  sources = [
    "application_manager_apptests.mojom",
  ]
}

mojo_application_manifest("manifest") {
  application_name = "mojo_shell_apptests"
  source = "application_manager_apptest_manifest.json"
}

executable("application_manager_apptest_driver") {
  testonly = true

  sources = [
    "application_manager_apptest_driver.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//base:base_static",
    "//build/config/sanitizers:deps",
    "//mojo/common:common_base",
    "//mojo/edk/system",
    "//mojo/shell/public/cpp",
    "//mojo/shell/public/interfaces",
    "//mojo/shell/runner:init",
    "//mojo/shell/runner/child:test_native_main",
    "//mojo/shell/runner/common",
  ]
}

executable("application_manager_apptest_target") {
  testonly = true

  sources = [
    "application_manager_apptest_target.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//build/config/sanitizers:deps",
    "//mojo/common:common_base",
    "//mojo/shell/public/cpp",
    "//mojo/shell/runner/child:test_native_main",
  ]
}
