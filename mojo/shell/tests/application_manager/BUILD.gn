# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

source_set("application_manager") {
  testonly = true
  sources = [
    "application_manager_unittest.cc",
  ]

  deps = [
    ":interfaces",
    ":manifest",
    "//base",
    "//base/test:test_config",
    "//mojo/common:common_base",
    "//mojo/shell/public/cpp:shell_test_support",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/interfaces",
  ]

  data_deps = [
    ":application_manager_unittest_driver",
    ":application_manager_unittest_target",
  ]
}

mojom("interfaces") {
  sources = [
    "application_manager_unittest.mojom",
  ]
}

mojo_application_manifest("manifest") {
  application_name = "application_manager_unittest"
  source = "application_manager_unittest_manifest.json"
}

executable("application_manager_unittest_driver") {
  testonly = true

  sources = [
    "driver.cc",
  ]

  deps = [
    ":driver_manifest",
    ":interfaces",
    "//base",
    "//mojo/edk/system",
    "//mojo/shell/public/cpp",
    "//mojo/shell/public/interfaces",
    "//mojo/shell/runner:init",
    "//mojo/shell/runner/child:test_native_main",
    "//mojo/shell/runner/common",
  ]
}

mojo_application_manifest("driver_manifest") {
  type = "exe"
  application_name = "application_manager_unittest_driver"
  source = "driver_manifest.json"
}

executable("application_manager_unittest_target") {
  testonly = true

  sources = [
    "target.cc",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//mojo/shell/public/cpp",
    "//mojo/shell/runner/child:test_native_main",
  ]
}
