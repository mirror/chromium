# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

source_set("lifecycle") {
  testonly = true
  sources = [
    "lifecycle_unittest.cc",
  ]
  deps = [
    ":interfaces",
    ":manifest",
    "//base",
    "//base/test:test_support",
    "//mojo/shell/public/cpp:shell_test_support",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/interfaces",
  ]

  data_deps = [
    ":lifecycle_unittest_app",
    ":lifecycle_unittest_package",
  ]
}

mojom("interfaces") {
  sources = [
    "lifecycle_unittest.mojom",
  ]
}

mojo_application_manifest("manifest") {
  application_name = "lifecycle_unittest"
  source = "lifecycle_unittest_manifest.json"
}

mojo_application_manifest("lifecycle_unittest_package_app_a_manifest") {
  application_name = "lifecycle_unittest_package_app_a"
  source = "package_app_a_manifest.json"
}

mojo_application_manifest("lifecycle_unittest_package_app_b_manifest") {
  application_name = "lifecycle_unittest_package_app_b"
  source = "package_app_b_manifest.json"
}

source_set("app_client") {
  sources = [
    "app_client.cc",
    "app_client.h",
  ]
  deps = [
    ":interfaces",
    "//base",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/interfaces",
  ]
}

mojo_native_application("lifecycle_unittest_package") {
  testonly = true
  sources = [
    "package.cc",
  ]
  deps = [
    ":app_client",
    ":interfaces",
    ":lifecycle_unittest_package_manifest",
    "//base",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/interfaces",
  ]
}

mojo_application_manifest("lifecycle_unittest_package_manifest") {
  application_name = "lifecycle_unittest_package"
  source = "package_manifest.json"
  deps = [
    ":lifecycle_unittest_package_app_a_manifest",
    ":lifecycle_unittest_package_app_b_manifest",
  ]
  packaged_applications = [
    "lifecycle_unittest_package_app_a",
    "lifecycle_unittest_package_app_b",
  ]
}

mojo_native_application("lifecycle_unittest_app") {
  testonly = true
  sources = [
    "app.cc",
  ]
  deps = [
    ":app_client",
    ":interfaces",
    ":lifecycle_unittest_app_manifest",
    "//base",
    "//mojo/shell/public/cpp:sources",
  ]
}

mojo_application_manifest("lifecycle_unittest_app_manifest") {
  application_name = "lifecycle_unittest_app"
  source = "app_manifest.json"
}
