# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//extensions/features/features.gni")

assert(enable_extensions,
       "Cannot depend on extensions because enable_extensions=false.")

import("//build/config/features.gni")

source_set("networking_onc") {
  sources = [
    "networking_cast_private_delegate.cc",
    "networking_cast_private_delegate.h",
    "networking_onc_api.cc",
    "networking_onc_api.h",
    "networking_onc_delegate.cc",
    "networking_onc_delegate.h",
    "networking_onc_delegate_factory.cc",
    "networking_onc_delegate_factory.h",
    "networking_onc_delegate_observer.h",
    "networking_onc_event_router.h",
    "networking_onc_event_router_factory.cc",
    "networking_onc_event_router_factory.h",
  ]

  deps = [
    "//extensions/common/api",
    "//net",
  ]

  public_deps = [
    "//extensions/browser:browser_sources",
  ]

  if (!is_chromeos && is_linux) {
    sources += [
      "network_config_dbus_constants_linux.cc",
      "network_config_dbus_constants_linux.h",
      "networking_onc_event_router_nonchromeos.cc",
      "networking_onc_linux.cc",
      "networking_onc_linux.h",
    ]
    if (use_dbus) {
      deps += [ "//dbus" ]
    }
  } else if (is_win || is_mac) {
    sources += [
      "networking_onc_event_router_nonchromeos.cc",
      "networking_onc_service_client.cc",
      "networking_onc_service_client.h",
    ]
    deps += [ "//components/wifi" ]
  } else if (is_chromeos) {
    sources += [
      "networking_onc_chromeos.cc",
      "networking_onc_chromeos.h",
      "networking_onc_event_router_chromeos.cc",
    ]
    deps += [ "//chromeos" ]
  }
}
