# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//extensions/extensions.gni")
import("//mojo/public/tools/bindings/mojom.gni")

source_set("common_constants") {
  sources =
      rebase_path(extensions_gypi_values.extensions_common_constants_sources,
                  ".",
                  "//extensions")

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]
}

if (enable_extensions) {
  mojom("mojo") {
    sources = rebase_path(extensions_gypi_values.extensions_common_mojo_sources,
                          ".",
                          "//extensions")
    if (proprietary_codecs && enable_wifi_display) {
      wifi_display_sources = rebase_path(
              extensions_gypi_values.extensions_common_mojo_sources_wifi_display,
              ".",
              "//extensions")
      sources += wifi_display_sources
    }

    use_new_wrapper_types = false
  }

  # GYP version: extensions/extensions.gyp:extensions_common
  # This must be a static library because extensions common depends on
  # GetTrustedICAPublicKey in extensions/browser which isn't always linked
  # in. TODO(brettw): This reverse dependency should be fixed.
  static_library("common") {
    sources = rebase_path(extensions_gypi_values.extensions_common_sources,
                          ".",
                          "//extensions")

    configs += [
      "//build/config:precompiled_headers",

      # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
      "//build/config/compiler:no_size_t_to_int_warning",
    ]

    public_deps = [
      ":common_constants",
      ":mojo",
    ]

    deps = [
      "//base",

      # TODO(benwells): figure out what to do with the api target and
      # api resources compiled into the chrome resource bundle.
      # http://crbug.com/162530
      "//chrome:resources",
      "//components/crx_file",
      "//components/url_matcher",
      "//content/public/common",
      "//crypto",
      "//device/bluetooth",
      "//device/usb",
      "//extensions:extensions_resources",
      "//extensions/common/api",
      "//extensions/strings",
      "//ipc",
      "//net",
      "//third_party/boringssl",
      "//third_party/icu",
      "//third_party/libxml",
      "//third_party/re2",
      "//ui/base",
      "//ui/gfx/geometry",
      "//ui/gfx/ipc",
      "//ui/gfx/ipc/geometry",
      "//ui/gfx/ipc/skia",
      "//url",
    ]

    if (enable_nacl) {
      nacl_sources =
          rebase_path(extensions_gypi_values.extensions_common_sources_nacl,
                      ".",
                      "//extensions")
      sources += nacl_sources
    }
  }

  source_set("unit_tests") {
    testonly = true
    sources = [
      "api/declarative/declarative_manifest_unittest.cc",
      "api/printer_provider/usb_printer_manifest_unittest.cc",
      "api/sockets/sockets_manifest_permission_unittest.cc",
      "csp_validator_unittest.cc",
      "event_filter_unittest.cc",
      "extension_l10n_util_unittest.cc",
      "extension_messages_unittest.cc",
      "extension_resource_unittest.cc",
      "extension_set_unittest.cc",
      "features/base_feature_provider_unittest.cc",
      "features/complex_feature_unittest.cc",
      "features/simple_feature_unittest.cc",
      "file_util_unittest.cc",
      "image_util_unittest.cc",
      "manifest_handler_unittest.cc",
      "manifest_handlers/content_capabilities_manifest_unittest.cc",
      "manifest_handlers/default_locale_manifest_unittest.cc",
      "manifest_handlers/externally_connectable_unittest.cc",
      "manifest_handlers/file_handler_manifest_unittest.cc",
      "manifest_handlers/icons_handler_unittest.cc",
      "manifest_handlers/incognito_manifest_unittest.cc",
      "manifest_handlers/kiosk_mode_info_unittest.cc",
      "manifest_handlers/oauth2_manifest_unittest.cc",
      "manifest_handlers/shared_module_manifest_unittest.cc",
      "message_bundle_unittest.cc",
      "one_shot_event_unittest.cc",
      "permissions/api_permission_set_unittest.cc",
      "permissions/manifest_permission_set_unittest.cc",
      "permissions/socket_permission_unittest.cc",
      "permissions/usb_device_permission_unittest.cc",
      "stack_frame_unittest.cc",
      "update_manifest_unittest.cc",
      "url_pattern_set_unittest.cc",
      "url_pattern_unittest.cc",
      "user_script_unittest.cc",
      "value_builder_unittest.cc",
    ]

    deps = [
      ":common",
      "//base",
      "//base:i18n",
      "//base/test:test_support",
      "//components/crx_file",
      "//device/usb",
      "//extensions:extensions_resources",

      # TODO(brettw) these tests should not be including headers from browser.
      "//extensions:test_support",
      "//extensions/browser",
      "//extensions/strings",
      "//ipc",
      "//testing/gmock",
      "//testing/gtest",
      "//ui/base",
      "//ui/gfx",
      "//url",
    ]
  }
}  # enable_extensions
