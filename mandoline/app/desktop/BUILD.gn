# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("desktop") {
  deps = [
    ":mandoline",
  ]
}

executable("mandoline") {
  sources = [
    "../core_services_initialization.cc",
    "launcher_process.cc",
    "launcher_process.h",
    "main.cc",
  ]

  deps = [
    "//base",
    "//build/config/sanitizers:deps",
    "//components/tracing:startup_tracing",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/runner:lib",
  ]

  if (is_linux && !is_android) {
    sources += [
      "linux_sandbox.cc",
      "linux_sandbox.h",
    ]

    deps += [ "//sandbox/linux:sandbox" ]
  }

  data_deps = [
    "//components/html_viewer",
    "//mandoline/services/core_services",
    "//mandoline/services/updater",
    "//mandoline/ui",
  ]
}
