import("//build/config/compiler/compiler.gni")

action("make_s2_language_locator") {
  script = "convert_s2_cell.py"
  sources = [
    "adminregionsdata.csv",
  ]
  inputs = [
    "template/language_code_locator_helper.cc.tmpl",
  ]
  outputs = [
    "$target_gen_dir/language_code_locator_helper.cc",
  ]

  args = [ "-o" ] + rebase_path(outputs, root_build_dir)
  args += [ "-t" ] + rebase_path(inputs, root_build_dir)
  args += [ "-i" ] + rebase_path(sources, root_build_dir)
}

source_set("util") {
  sources = [
              "language_code_locator.h",
              "language_code_locator.cc",
            ] + get_target_outputs(":make_s2_language_locator")
  deps = [
    ":make_s2_language_locator",
    "//base",
    "//third_party/s2cellid",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "language_code_locator_unittest.cc",
  ]
  deps = [
    ":util",
    "//base",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
