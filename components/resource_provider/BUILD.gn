# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//mojo/mojo_application_package.gni")

mojo_native_application("resource_provider") {
  sources = [
    "main.cc",
    "resource_provider_app.cc",
    "resource_provider_app.h",
  ]

  deps = [
    ":lib",
    "//base",
    "//components/resource_provider/public/interfaces",
    "//mojo/application",
    "//mojo/environment:chromium",
    "//url",
  ]
}

source_set("lib") {
  sources = [
    "file_utils.cc",
    "file_utils.h",
    "resource_provider_impl.cc",
    "resource_provider_impl.h",
  ]

  deps = [
    "//base",
    "//components/resource_provider/public/interfaces",
    "//mojo/common:common_base",
    "//mojo/platform_handle",
    "//url",
  ]
}

test("tests") {
  output_name = "resource_provider_unittests"
  sources = [
    "file_utils_unittest.cc",
  ]

  deps = [
    ":lib",
    "//base",
    "//base/test:test_config",
    "//components/resource_provider/public/interfaces",
    "//mojo/environment:chromium",
    "//testing/gtest",
    "//third_party/mojo/src/mojo/edk/test:run_all_unittests",
    "//url",
  ]
}

mojo_application_package("apptests") {
  output_name = "resource_provider_apptests"
  testonly = true

  sources = [
    "resource_provider_apptest.cc",
  ]

  resources = [
    "//components/test/data/resource_provider/sample",
    "//components/test/data/resource_provider/dir",
  ]

  deps = [
    "//base",
    "//base/test:test_config",
    "//components/resource_provider/public/interfaces",
    "//mojo/application",
    "//mojo/application:test_support",
    "//mojo/common",
    "//mojo/platform_handle",
    "//third_party/mojo/src/mojo/public/cpp/bindings:bindings",
  ]
}
