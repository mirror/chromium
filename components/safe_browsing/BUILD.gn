# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

proto_library("csd_proto") {
  sources = [
    "csd.proto",
  ]
}

source_set("safe_browsing") {
  sources = [
    "base_blocking_page.cc",
    "base_blocking_page.h",
    "base_resource_throttle.cc",
    "base_resource_throttle.h",
    "base_ui_manager.cc",
    "base_ui_manager.h",
  ]
  public_deps = [
    "//components/security_interstitials/content:security_interstitial_page",
  ]
  deps = [
    ":base_ping_manager",
    ":features",
    "//base:base",
    "//base:i18n",
    "//components/safe_browsing/common:safe_browsing_prefs",
    "//components/safe_browsing/web_ui:constants",
    "//components/safe_browsing_db:database_manager",
    "//components/security_interstitials/core:core",
    "//content/public/browser:browser",
    "//content/public/common:common",
    "//net:net",
  ]
}

static_library("base_ping_manager") {
  sources = [
    "base_ping_manager.cc",
    "base_ping_manager.h",
  ]

  public_deps = [
    "//google_apis:google_apis",
  ]

  deps = [
    "//base:base",
    "//components/data_use_measurement/core:core",
    "//components/safe_browsing_db:hit_report",
    "//components/safe_browsing_db:util",
    "//content/public/browser:browser",
    "//net:net",
  ]
}

source_set("base_ping_manager_unittest") {
  testonly = true
  sources = [
    "base_ping_manager_unittest.cc",
  ]

  deps = [
    ":base_ping_manager",
    "//base:base",
    "//net:net",
    "//net:test_support",
    "//testing/gtest",
  ]
}

source_set("safe_browsing_api_handler") {
  sources = [
    "android/jni_registrar.cc",
    "android/jni_registrar.h",
    "safe_browsing_api_handler.cc",
    "safe_browsing_api_handler.h",
  ]
  deps = [
    "//base",
    "//components/safe_browsing_db:safe_browsing_api_handler_util",
    "//components/safe_browsing_db:util",
    "//content/public/browser:browser",
    "//url",
  ]

  if (is_android) {
    deps += [ "//components/safe_browsing/android:jni_headers" ]
    sources += [
      "android/safe_browsing_api_handler_bridge.cc",
      "android/safe_browsing_api_handler_bridge.h",
    ]
  }
}

static_library("features") {
  sources = [
    "features.cc",
    "features.h",
  ]

  deps = [
    "//base:base",
  ]
}
