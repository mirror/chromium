# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
}

component("sessions") {
  sources = [
    # TODO(rohitrao): Split this target into three separate core, content,
    # and web targets.
    "content/content_serialized_navigation_builder.cc",
    "content/content_serialized_navigation_builder.h",
    "content/content_serialized_navigation_driver.cc",
    "content/content_serialized_navigation_driver.h",
    "core/serialized_navigation_driver.h",
    "serialized_navigation_entry.cc",
    "serialized_navigation_entry.h",
    "session_id.cc",
    "session_id.h",
  ]

  defines = [ "SESSIONS_IMPLEMENTATION" ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//content/public/browser",
    "//skia",
    "//ui/base",
    "//url",
  ]

  if (!is_android || !is_android_webview_build) {
    deps += [ "//sync" ]
  }
}

static_library("test_support") {
  testonly = true
  sources = [
    "serialized_navigation_entry_test_helper.cc",
    "serialized_navigation_entry_test_helper.h",
  ]

  deps = [
    "//skia",
    "//testing/gtest",
  ]

  if (!is_android || !is_android_webview_build) {
    deps += [ "//sync" ]
  }
}
