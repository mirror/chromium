# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/cronet/native/include/headers.gni")
import("//components/grpc_support/include/headers.gni")
import("//testing/test.gni")

config("cronet_native_include_config") {
  include_dirs = [
    "//components/cronet/native/generated",
    "//components/cronet/native/include",
    "//components/grpc_support/include",
  ]
}

# Cronet native API implementation.
source_set("cronet_native_impl") {
  deps = [
    "//base",
    "//components/cronet:cronet_common",
    "//components/cronet:cronet_version_header",
    "//components/grpc_support:grpc_support",
    "//net",
  ]

  configs += [ ":cronet_native_include_config" ]
  public_configs = [ ":cronet_native_include_config" ]

  sources = [
    "buffer.cc",
    "engine.cc",
    "engine.h",
    "include/cronet_c.h",
    "runnables.cc",
    "runnables.h",
    "url_request.cc",
    "url_request.h",

    # Generated from cronet.idl.
    "generated/cronet.idl_c.h",
    "generated/cronet.idl_impl_interface.cc",
    "generated/cronet.idl_impl_interface.h",
    "generated/cronet.idl_impl_struct.cc",
    "generated/cronet.idl_impl_struct.h",
  ]
}

source_set("cronet_native_testutil") {
  visibility = [ ":*" ]
  testonly = true

  deps = [
    "//base",
    "//net:test_support",
  ]

  configs += [ ":cronet_native_include_config" ]
  public_configs = [ ":cronet_native_include_config" ]

  sources = [
    "test_url_request_callback.cc",
    "test_url_request_callback.h",
    "test_util.cc",
    "test_util.h",
  ]
}

# Unit tests for Cronet native API. Depends on cronet_native_impl to test
# implementation details.
source_set("cronet_native_unittests") {
  testonly = true

  deps = [
    ":cronet_native_impl",
    ":cronet_native_testutil",
    "//net:test_support",
  ]

  configs += [ ":cronet_native_include_config" ]

  sources = [
    "runnables_unittest.cc",

    # Generated from cronet.idl.
    "generated/cronet.idl_impl_interface_unittest.cc",
    "generated/cronet.idl_impl_struct_unittest.cc",
  ]
}

# Tests for publicly exported Cronet Native API. This target does NOT depend on
# cronet_native_impl to prevent static linking of implementation into test app.
source_set("cronet_native_tests") {
  testonly = true

  deps = [
    ":cronet_native_testutil",
    "//base",
    "//base/test:test_support",
    "//components/cronet/test:test_support",
    "//components/grpc_support:bidirectional_stream_unittest",
    "//components/grpc_support/test:get_stream_engine_header",
    "//net:test_support",
    "//testing/gtest",
  ]

  configs += [ ":cronet_native_include_config" ]

  sources = [
    "buffer_test.cc",
    "engine_test.cc",
    "executors_test.cc",
    "test_stream_engine.cc",
    "url_request_test.cc",
  ]
}
