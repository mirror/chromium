# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/mojo/src/mojo/public/mojo_application.gni")

mojo_native_application("filesystem") {
  sources = [
    "futimens.h",
    "main.cc",
    "util.cc",
    "util.h",
  ]

  if (is_android) {
    sources += [ "futimens_android.cc" ]
  }

  if (is_posix) {
    sources += [
      "posix/directory_posix.cc",
      "posix/directory_posix.h",
      "posix/file_posix.cc",
      "posix/file_posix.h",
      "posix/file_system_posix.cc",
      "posix/file_system_posix.h",
      "posix/shared_posix.cc",
      "posix/shared_posix.h",
    ]
  }

  if (is_win) {
    sources += [
      "win/file_system_win.cc",
      "win/file_system_win.h",
    ]
  }

  deps = [
    "//base",
    "//components/filesystem/public/interfaces",
    "//mojo/application/public/cpp",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//third_party/mojo/src/mojo/public/cpp/system",
  ]
}

mojo_native_application("apptests") {
  output_name = "files_apptests"

  testonly = true

  sources = [
    "directory_unittest.cc",
    "file_unittest.cc",
    "files_test_base.cc",
    "files_test_base.h",
  ]

  deps = [
    "//base",
    "//components/filesystem/public/interfaces",
    "//mojo/application/public/cpp:test_support",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
  ]
}
