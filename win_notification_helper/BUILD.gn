# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//chrome/process_version_rc_template.gni")

executable("win_notification_helper") {
  sources = [
    "win_notification_helper.cc",
    "win_notification_helper.rc",
  ]

  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]

  deps = [
    ":lib",
    ":version_resources",
    "//build/win:default_exe_manifest",
    "//chrome/install_static:primary_module",
  ]

  libs = [ "runtimeobject.lib" ]

  ldflags = [
    "/DELAYLOAD:api-ms-win-core-winrt-error-l1-1-0.dll",
    "/DELAYLOAD:api-ms-win-core-winrt-l1-1-0.dll",
    "/DELAYLOAD:api-ms-win-core-winrt-string-l1-1-0.dll",
  ]
}

source_set("lib") {
  visibility = [ ":*" ]
  sources = [
    "notification_activator.cc",
    "notification_activator.h",
    "notification_activator_registrar.cc",
    "notification_activator_registrar.h",
  ]

  public_deps = [
    "//base",
    "//chrome/common:constants",
    "//chrome/install_static:install_static_util",
    "//components/crash/content/app:run_as_crashpad_handler",
    "//content/public/common:static_switches",
  ]
}

process_version_rc_template("version_resources") {
  sources = [
    "win_notification_helper_exe.ver",
  ]
  output = "$target_gen_dir/win_notification_helper_exe.rc"
}
