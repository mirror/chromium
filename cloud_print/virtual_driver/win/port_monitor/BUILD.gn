# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/process_version_rc_template.gni")

assert(is_win)

group("port_monitor") {
  public_deps = [
    ":port_monitor_dll",
  ]
}

shared_library("port_monitor_dll") {
  output_name = "gcp_portmon"

  sources = [
    "port_monitor.def",
    "port_monitor_dll.cc",
  ]

  deps = [
    ":lib",
    ":resources",
    "//base",
    "//chrome/common:constants",
    "//chrome/common:version_header",
    "//cloud_print/common",
    "//cloud_print/virtual_driver/win",
  ]

  libs = [ "userenv.lib" ]
}

source_set("lib") {
  sources = [
    "port_monitor.cc",
    "port_monitor.h",
  ]

  deps = [
    "//base",
    "//chrome/common:constants",
    "//chrome/installer/launcher_support",
    "//cloud_print/common",
    "//cloud_print/virtual_driver/win",
  ]
}

process_version_rc_template("resources") {
  sources = [
    "../gcp_portmon_dll.ver",
  ]

  # Note: target_gen_dir will be different for each toolchain so the output
  # name doesn't need mangling.
  output = "$target_gen_dir/gcp_portmon_dll.rc"
}
