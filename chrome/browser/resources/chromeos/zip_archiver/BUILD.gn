# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

group("zip_archiver") {
  deps = [
    ":zip_archiver_css",
    ":zip_archiver_html",
    ":zip_archiver_icons",
    ":zip_archiver_js",
    ":zip_archiver_root",
  ]
}

zip_archiver_dir="$root_out_dir/resources/chromeos/zip_archiver"

copy("zip_archiver_css") {
  sources = [
    "css/passphrase.css",
    "css/passphrase-dialog.css",
  ]

  outputs = [
    "$zip_archiver_dir/css/{{source_file_part}}",
  ]
}

copy("zip_archiver_html") {
  sources = [
    "html/compressor.html",
    "html/passphrase.html",
    "html/passphrase-dialog.html",
  ]

  outputs = [
    "$zip_archiver_dir/html/{{source_file_part}}",
  ]
}

copy("zip_archiver_icons") {
  sources = [
    "icons/icon16.png",
    "icons/icon32.png",
    "icons/icon64.png",
    "icons/icon96.png",
    "icons/icon128.png",
  ]

  outputs = [
    "$zip_archiver_dir/icons/{{source_file_part}}",
  ]
}

copy("zip_archiver_js") {
  sources = [
    "js/app.js",
    "js/background.js",
    "js/build-config.js",
    "js/compressor.js",
    "js/compressor-foreground.js",
    "js/decompressor.js",
    "js/passphrase-dialog.js",
    "js/passphrase-manager.js",
    "js/request.js",
    "js/types.js",
    "js/unpacker.js",
    "js/volume.js",
  ]

  outputs = [
    "$zip_archiver_dir/js/{{source_file_part}}",
  ]
}

copy("zip_archiver_root") {
  pexe_dir = get_label_info(
      "//chrome/browser/resources/chromeos/zip_archiver/cpp:zip_archiver_pnacl(//build/toolchain/nacl:newlib_pnacl)",
      "root_out_dir")

  deps = [
    "//chrome/browser/resources/chromeos/zip_archiver/cpp:zip_archiver_pnacl(//build/toolchain/nacl:newlib_pnacl)",
  ]

  sources = [
    "manifest.json",
    "module.nmf",
    "$pexe_dir/zip_archiver_pnacl.pexe",
  ]

  outputs = [
    "$zip_archiver_dir/{{source_file_part}}",
  ]
}
