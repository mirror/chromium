import("src/files.gni")

group("all") {
  deps = [
    ":media_router",
  ]
}

# Run JSCompiler for typechecking.  When Java is not available, this
# build step succeeds without doing any typechecking.
action("media_router_type_check") {
  script = "typecheck.py"
  inputs = rebase_path(mr_module_files, ".", "src") + [
             "src/externs.js",
             "src/mojo_externs.js",
           ]
  outputs = [
    target_gen_dir + "/media_router.stamp",
  ]
  args = [
    "--compile-script",
    rebase_path("//third_party/closure_compiler/compile2.py", root_build_dir),
  ]
  args += [ "--out-file" ] + rebase_path(outputs, root_build_dir)
  args += [ "--" ] + rebase_path(inputs, root_build_dir)
}

# Concatentate JS files to produce "module" JS files that can be
# loaded at runtime.  This could be done by JSCompiler, but it depends
# on Java, and Java isn't always available.
action("media_router_modules") {
  script = "concat_js_modules.py"
  module_inputs = rebase_path(mr_module_files, ".", "src")
  inputs = module_inputs + [ "prelude.js" ]
  outputs = []
  foreach(module_name, mr_module_names) {
    outputs += [ "${target_gen_dir}/${module_name}.js" ]
  }
  args = [ "--module-specs" ] + mr_module_specs + [
           "--prelude-file",
           rebase_path("prelude.js", root_build_dir),
           "--output-dir",
           rebase_path(target_gen_dir + "/", root_build_dir),
           "--",
         ] + rebase_path(module_inputs, root_build_dir)
}

# Produce the Media Router extension.  At present, the extension isn't
# included in the Chromium distribution, but it can be sideloaded into
# Chromium for testing.
action("media_router") {
  script = "assemble_extension.py"
  inputs = [
    "manifest.yaml",
  ]
  outputs = [
    "$target_gen_dir/manifest.json",
  ]
  deps = [
    ":media_router_modules",
    ":media_router_type_check",
  ]
  args = [
    "--manifest_in=" + rebase_path("manifest.yaml", root_build_dir),
    "--output_dir=" + rebase_path(target_gen_dir, root_build_dir),
  ]
}
