import("../vulcanize.gni")
import("//tools/grit/grit_rule.gni")
import("//chrome/common/features.gni")

vulcanized_unbuilt = "vulcanized.unbuilt.html"
settings_pak_file = "settings_resources.pak"
unpak_folder = "settings_resources.unpak"

vulcanize("vulcanize") {
  host = "md-settings"
  html_in_file = "settings.html"
  html_out_file = vulcanized_unbuilt
  insert_in_head = "<base href=\"chrome://\$i18n{hostname}\">"
  input = rebase_path("$target_gen_dir/$unpak_folder", root_build_dir)
  js_out_file = "crisper.js"

  deps = [
    ":unpak",
  ]
}

unpak("unpak") {
  pak_file = settings_pak_file
  out_folder = unpak_folder

  deps = [
    ":flattened_resources",
  ]
}

polymer_css_build("build") {
  input_files = [ vulcanized_unbuilt ]
  output_files = [ "vulcanized.html" ]
  deps = [
    ":vulcanize",
  ]
}

grit("flattened_resources") {
  source = "settings_resources.grd"
  defines = chrome_grit_defines
  outputs = [
    "grit/settings_resources.h",
    "grit/settings_resources_map.cc",
    "grit/settings_resources_map.h",
    settings_pak_file,
  ]
  output_dir = "$root_gen_dir/chrome/browser/resources/settings"
}
