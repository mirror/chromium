# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

declare_args() {
  # Enables blimp. This is required to get the real implementation of blimp.
  # By default, only a dummy-implementation is provided.
  enable_blimp = false
}

# This target is here to let //chrome depend on //blimp/client/public
# and automatically get the correct implementation of //blimp/client/core
# depending on whether enable_blimp is set or not.
group("core_shim") {
  visibility = [ "//blimp/client/public" ]

  if (enable_blimp) {
    deps = [
      ":core",
    ]
  } else {
    deps = [
      ":dummy_core",
    ]
  }
}

source_set("core") {
  visibility = [ "//blimp/client/*" ]

  sources = [
    "blimp_client_context_impl.cc",
    "blimp_client_context_impl.h",
    "blimp_contents_impl.cc",
    "blimp_contents_impl.h",
    "blimp_navigation_controller_delegate.h",
    "blimp_navigation_controller_impl.cc",
    "blimp_navigation_controller_impl.h",
  ]

  deps = [
    "//base",
    "//blimp/client/public:public_headers",
    "//url",
  ]

  if (is_android) {
    sources += [
      "android/blimp_client_context_impl_android.cc",
      "android/blimp_client_context_impl_android.h",
      "android/blimp_contents_factory.cc",
      "android/blimp_contents_factory.h",
      "android/blimp_contents_impl_android.cc",
      "android/blimp_contents_impl_android.h",
      "android/blimp_contents_observer_proxy.cc",
      "android/blimp_contents_observer_proxy.h",
      "android/blimp_jni_registrar.cc",
      "android/blimp_navigation_controller_impl_android.cc",
      "android/blimp_navigation_controller_impl_android.h",
    ]

    deps += [ ":core_jni_headers" ]
  }
}

source_set("dummy_core") {
  visibility = [ ":*" ]

  sources = [
    "dummy_blimp_client_context.cc",
    "dummy_blimp_client_context.h",
  ]

  deps = [
    "//base",
    "//blimp/client/public:public_headers",
    "//url",
  ]

  if (is_android) {
    sources += [
      "android/dummy_blimp_client_context_android.cc",
      "android/dummy_blimp_client_context_android.h",
      "android/dummy_blimp_jni_registrar.cc",
    ]

    deps += [ ":dummy_core_jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "blimp_client_context_impl_unittest.cc",
    "blimp_contents_impl_unittest.cc",
    "blimp_navigation_controller_impl_unittest.cc",
  ]

  deps = [
    ":core",
    "//base",
    "//blimp/client/public:public_headers",
    "//blimp/client/test",
    "//testing/gtest",
    "//url",
  ]
}

source_set("switches") {
  visibility = [
    "//blimp/client/*",
    "//blimp/engine:browser_tests",
  ]

  sources = [
    "blimp_client_switches.cc",
    "blimp_client_switches.h",
  ]
}

if (is_android) {
  # This target is here to let //chrome depend on
  # //blimp/client/public:public_java and automatically get the correct
  # implementation of //blimp/client/core depending on whether enable_blimp is
  # set or not.
  java_group("core_shim_java") {
    visibility = [ "//blimp/client/public/*" ]

    if (enable_blimp) {
      deps = [
        ":core_java",
      ]
    } else {
      deps = [
        ":dummy_core_java",
      ]
    }
  }

  android_library("core_java") {
    visibility = [ "//blimp/client/*" ]

    java_files = [
      "android/java/src/org/chromium/blimp/core/BlimpClientContextImpl.java",
      "android/java/src/org/chromium/blimp/core/BlimpClientSwitches.java",
      "android/java/src/org/chromium/blimp/core/BlimpContentsFactory.java",
      "android/java/src/org/chromium/blimp/core/BlimpContentsImpl.java",
      "android/java/src/org/chromium/blimp/core/BlimpContentsObserverProxy.java",
      "android/java/src/org/chromium/blimp/core/BlimpNavigationControllerImpl.java",
    ]

    deps = [
      "//base:base_java",
      "//blimp/client/public:public_headers_java",
    ]
  }

  android_library("dummy_core_java") {
    visibility = [ ":*" ]

    java_files = [ "android/java/src/org/chromium/blimp/core/DummyBlimpClientContext.java" ]

    deps = [
      "//base:base_java",
      "//blimp/client/public:public_headers_java",
    ]
  }

  generate_jni("core_jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/blimp/core/BlimpClientContextImpl.java",
      "android/java/src/org/chromium/blimp/core/BlimpContentsFactory.java",
      "android/java/src/org/chromium/blimp/core/BlimpContentsImpl.java",
      "android/java/src/org/chromium/blimp/core/BlimpContentsObserverProxy.java",
      "android/java/src/org/chromium/blimp/core/BlimpNavigationControllerImpl.java",
    ]

    jni_package = "blimp/client/core"
  }

  generate_jni("dummy_core_jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/blimp/core/DummyBlimpClientContext.java",
    ]

    jni_package = "blimp/client/core"
  }
}
