# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")

# Things needed by multiple targets, like renderer_impl and renderer_app.
# GYP version: media/media.gyp:media_mojo_lib
source_set("lib") {
  deps = [
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/converters/geometry",
    "//mojo/environment:chromium",
    "//mojo/public/c/system:for_component",
    "//mojo/services/public/interfaces/geometry",
    "//skia",
  ]

  sources = [
    "media_type_converters.cc",
    "media_type_converters.h",
    "mojo_demuxer_stream_adapter.cc",
    "mojo_demuxer_stream_adapter.h",
  ]
}

# mojo media::Renderer proxy (to a renderer_app) implementation.
source_set("renderer_impl_lib") {
  deps = [
    ":lib",
    "//base",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/public/interfaces/application",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/public/c/system:for_component",
    "//mojo/public/cpp/application",
  ]

  sources = [
    "mojo_demuxer_stream_impl.cc",
    "mojo_demuxer_stream_impl.h",
    "mojo_renderer_impl.cc",
    "mojo_renderer_impl.h",
  ]
}

# mojo media::Renderer application.
# GYP version: media/media.gyp:mojo_media_renderer_app
mojo_native_application("renderer_app") {
  output_name = "mojo_media_renderer_app"

  deps = [
    "//base",
    "//media",
    "//media:shared_memory_support",
    "//media/mojo/interfaces",
    "//mojo/common",
    "//mojo/application",
    "//mojo/public/c/system:for_shared_library",
    ":lib",
  ]

  sources = [
    "demuxer_stream_provider_shim.cc",
    "demuxer_stream_provider_shim.h",
    "mojo_renderer_service.cc",
    "mojo_renderer_service.h",
    "renderer_config.cc",
    "renderer_config.h",
    "renderer_config_default.cc",
  ]
}

test("mojo_media_lib_unittests") {
  sources = [
    "media_type_converters_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//media",
    "//media/mojo/interfaces",
    "//mojo/edk/system",
    "//mojo/edk/test:run_all_unittests",
    "//mojo/environment:chromium",
    "//testing/gtest",
    ":lib"
  ]
}

# Not a 'test' because this is loaded via mojo_shell as an app.
# To run the test:
#   out/Debug/mojo_shell mojo:mojo_media_renderer_apptest
# You may need to get "mojo_shell" from a mojo checkout and symlink all required
# libraries.
# TODO(msw): Fix GYP build for ApplicationTestBase so that we can add a GYP
# version of this test.
mojo_native_application("media_renderer_apptest") {
  testonly = true
  output_name = "mojo_media_renderer_apptest"

  deps = [
    "//base",
    "//media",
    "//media:shared_memory_support",
    "//media/mojo/interfaces",
    "//mojo/application",
    "//mojo/application:test_support",
    "//mojo/common",
    "//mojo/environment:chromium",
    ":renderer_impl_lib",
    ":renderer_app",
    ":lib",
    "//mojo/public/c/system:for_shared_library",
  ]

  sources = [
    "media_renderer_apptest.cc",
  ]
}

group("services") {
  deps = [
    ":lib",
    ":renderer_impl_lib",
    ":renderer_app",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":mojo_media_lib_unittests",
    ":media_renderer_apptest",
  ]
}
