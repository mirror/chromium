# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")
import("//chromecast/chromecast.gni")
import("//media/media_options.gni")

source_set("backend") {
  sources = [
    "audio_decoder_wrapper.cc",
    "audio_decoder_wrapper.h",
    "media_pipeline_backend_factory.h",
    "media_pipeline_backend_factory_impl.cc",
    "media_pipeline_backend_factory_impl.h",
    "media_pipeline_backend_manager.cc",
    "media_pipeline_backend_manager.h",
    "media_pipeline_backend_wrapper.cc",
    "media_pipeline_backend_wrapper.h",
  ]

  public_deps = [
    "//chromecast/public",
    "//chromecast/public/media",
  ]

  deps = [
    "//base",
    "//chromecast:chromecast_features",
    "//chromecast/media:libcast_media",
    "//chromecast/media/cma/base",
  ]
}

source_set("cast_media_android_dummy") {
  sources = [
    "cast_media_android_dummy.cc",
  ]
  deps = [
    "//base",
    "//chromecast:chromecast_features",
    "//chromecast/public",
    "//chromecast/public/media",
  ]
}

# Target for OEM partners to override media shared library, i.e.
# libcast_media_1.0.so. This target is only used to build executables
# with correct linkage information.
cast_shared_library("libcast_media_1.0") {
  if (is_cast_desktop_build) {
    deps = [
      ":default",
    ]
  } else {
    deps = [
      ":dummy",
    ]
  }
}

# Default implementation of media backend used on desktop.
source_set("default") {
  sources = [
    "audio_decoder_default.cc",
    "audio_decoder_default.h",
    "cast_media_default.cc",
    "media_pipeline_backend_default.cc",
    "media_pipeline_backend_default.h",
    "media_sink_default.cc",
    "media_sink_default.h",
    "video_decoder_default.cc",
    "video_decoder_default.h",
  ]

  deps = [
    "//base",
    "//chromecast/base",
    "//chromecast/public/media",
    "//media",
  ]
}

# Dummy implementation of media backend used on chromecast devices.
# Must not depend on anything outside //chromecast/public.
source_set("dummy") {
  sources = [
    "cast_media_dummy.cc",
  ]

  deps = [
    "//chromecast/public",
  ]
}

source_set("slew_volume") {
  sources = [
    "slew_volume.cc",
    "slew_volume.h",
  ]
  deps = [
    "//base",
    "//media:shared_memory_support",
  ]
}

source_set("common") {
  sources = [
    "cast_audio_json.cc",
    "cast_audio_json.h",
    "filter_group.cc",
    "filter_group.h",
    "post_processing_pipeline.h",
    "post_processing_pipeline_impl.cc",
    "post_processing_pipeline_impl.h",
    "post_processing_pipeline_parser.cc",
    "post_processing_pipeline_parser.h",
    "post_processor_factory.cc",
    "post_processor_factory.h",
    "stream_mixer.cc",
    "stream_mixer.h",
    "stream_mixer_input.cc",
    "stream_mixer_input.h",
    "stream_mixer_input_impl.cc",
    "stream_mixer_input_impl.h",
    "video_decoder_null.cc",
    "video_decoder_null.h",
    "volume_map.cc",
    "volume_map.h",
  ]

  deps = [
    ":slew_volume",
    "//base",
    "//chromecast/base",
    "//chromecast/public/media",
    "//media:shared_memory_support",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "filter_group_unittest.cc",
    "post_processors/governor_unittest.cc",
    "post_processors/post_processor_unittest.cc",
    "post_processors/post_processor_unittest.h",
    "post_processors/saturated_gain_unittest.cc",
    "slew_volume_unittests.cc",
  ]

  deps = [
    ":common",
    ":governor",
    ":saturated_gain",
    "//base/test:run_all_unittests",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (use_alsa) {
    deps += [ "alsa:unit_tests" ]
  }
  if (is_fuchsia) {
    deps += [ "fuchsia:unit_tests" ]
  }
}

test("cast_backend_unittests") {
  deps = [
    ":unit_tests",
  ]
}

if (use_alsa) {
  # cast_alsa_cma_backend_unittests is an alias for cast_backend_unittests.
  # TODO(sergeyu): Consider replace all references to this target with
  # cast_backend_unittests, then remove this target.
  test("cast_alsa_cma_backend_unittests") {
    deps = [
      ":unit_tests",
    ]
  }
}

# This is separate from ":governor" because the associated unittest needs
# to create a Governor directly (to use test functions).
cast_shared_library("libcast_governor_1.0") {
  sources = [
    "post_processors/governor_create.cc",
  ]
  deps = [
    ":governor",
    "//chromecast/public/media",
  ]
}

source_set("governor") {
  sources = [
    "post_processors/governor.cc",
    "post_processors/governor.h",
  ]
  deps = [
    ":slew_volume",
    "//base",
    "//chromecast/base",
    "//chromecast/public/media",
  ]
  public_configs = [ "//chromecast/public:public_config" ]
}

cast_shared_library("libcast_saturated_gain_1.0") {
  sources = [
    "post_processors/saturated_gain_create.cc",
  ]
  deps = [
    ":saturated_gain",
  ]
}

source_set("saturated_gain") {
  sources = [
    "post_processors/saturated_gain.cc",
    "post_processors/saturated_gain.h",
  ]
  deps = [
    ":slew_volume",
    "//base",
    "//chromecast/base",
    "//chromecast/public/media",
  ]
  public_configs = [ "//chromecast/public:public_config" ]
}
